class Board
{
    function void drawBorder()
    {
        do Screen.setColor(true);
        do Screen.drawLine(270, 20, 490, 20);
        do Screen.drawLine(270, 20, 270, 240);
        do Screen.drawLine(490, 20, 490, 240);
        do Screen.drawLine(270, 240, 490, 240);
        do Screen.drawRectangle(415, 25, 418, 230);
        do Screen.drawRectangle(340, 25, 343, 230);
        do Screen.drawRectangle(280, 85, 480, 88);
        do Screen.drawRectangle(280, 170, 480, 173);
        return;
    }
    function void drawSymbol(char symbol, int location)
    {
        var int xr, yr, x, y;
        var Array xCoordinates;
        var Array yCoordinates;
        // X-Coordinates.
        let xCoordinates = Array.new(9);
        let yCoordinates = Array.new(9);
        let xCoordinates[0] = 305;
        let xCoordinates[1] = 380;
        let xCoordinates[2] = 450;
        let xCoordinates[3] = 307;
        let xCoordinates[4] = 382;
        let xCoordinates[5] = 452;
        let xCoordinates[6] = 305;
        let xCoordinates[7] = 380;
        let xCoordinates[8] = 450;
        // Y-Coordinates.
        let yCoordinates[0] = 50;
        let yCoordinates[1] = 50;
        let yCoordinates[2] = 50;
        let yCoordinates[3] = 127;
        let yCoordinates[4] = 127;
        let yCoordinates[5] = 127;
        let yCoordinates[6] = 200;
        let yCoordinates[7] = 200;
        let yCoordinates[8] = 200;
        let xr = 20;
        let yr = 20;
        let x = xCoordinates[location];
        let y = yCoordinates[location];
        if (symbol = 79)
        {
        do Screen.setColor(true);
        do Screen.drawCircle(x, y, 25);
        do Screen.setColor(false);
        do Screen.drawCircle(x, y, 23);
        }
        if (symbol = 88)
        {
        do Screen.setColor(true);
        do Screen.drawLine(x - xr, y - yr, x + xr, y + yr);
        do Screen.drawLine(x - xr, y + yr, x + xr, y - yr);
        }
        return;
    }
    function Array createEmptyBoard()
    {
        var Array board;
        let board = Array.new(9);
        return board;
    }
    function boolean isWinning(Array board)
    {
        if((Board.equals3(board[0], board[1], board[2])) |
            (Board.equals3(board[3], board[4], board[5])) |
            (Board.equals3(board[6], board[7], board[8])) |
            (Board.equals3(board[0], board[3], board[6])) |
            (Board.equals3(board[1], board[4], board[7])) |
            (Board.equals3(board[2], board[5], board[8])))
        {
            return true;
        }
        if ((Board.equals3(board[0], board[4], board[8])) | (Board.equals3(board[2], board[4], board[6])))
        {
            return true;
        }
        return false;
    }
    function boolean equals3(int x, int y, int z)
    {
        if((x = 0) | (y = 0) | (z = 0))
        {
            return false;
        }
        if((x = y) & (y = z))
        {
            do Output.printChar(x);
            do Output.printString(" Wins!");
            do Output.println();
            return true;
        }
        return false;
    }
    function boolean isEmpty(Array board, int index) // Checks if a certain index is empty.
    {
        if (~(board[index] = 79) & ~(board[index] = 88))
        {
            return true;
        }
        else
        {
            return false;
        }
    }
    function int emptyCellsNum(Array board)
    {
        var int i, result;
        let i = 0;
        let result = 0;
        while(i<9)
        {
            if (Board.isEmpty(board, i))
            {
                let result = result + 1;
            }
            let i = i + 1;
        }
        return result;
    }
    function void userMove(Array board, char symbol)
    { 
        var int userchoice;
        var boolean played;
        let played = false;
        let userchoice = -1;
        while(~played)
        {
            while((userchoice < 0) | (userchoice > 8))
            {
                do Output.println();
                do Output.printString("Enter cell number [1 - 9]: ");
                let userchoice = Keyboard.readInt("") - 1;
            }
            if (Board.isEmpty(board, userchoice))
            {
                do Board.drawSymbol(symbol, userchoice);
                let board[userchoice] = symbol;
                let played = true;
            }
            else
            {
                do Output.println();
                do Output.printString("Cell Already Occupied.");
                do Output.println();
                let userchoice = -1;
            }
        }
        return;
    }
    function void randomMove(Array board, char computerSymbol)
    {
        var boolean played;
        var int randomIndex;
        let played = false;
        while(~played)
        {
            let randomIndex = Random.randRange(8);
            if (Board.isEmpty(board, randomIndex))
            {
                do Board.drawSymbol(computerSymbol, randomIndex);
                let board[randomIndex] = computerSymbol;
                let played = true;
            }
        }
        return;
    }
}   
