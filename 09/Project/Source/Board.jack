class Board
{
    field Array board;
    // Create a board object.
    constructor Board new()
    {
        let board = Array.new(9);
        do drawBorder();
        return this;
    }
    // Dispose currect object.
    method void dispose()
    {
        do Memory.deAlloc(this);
        return;
    }
    method void drawBorder()
    {   // Draws a game border.
        do Screen.setColor(true);
        do Screen.drawLine(270, 20, 490, 20);
        do Screen.drawLine(270, 20, 270, 240);
        do Screen.drawLine(490, 20, 490, 240);
        do Screen.drawLine(270, 240, 490, 240);
        do Screen.drawRectangle(415, 25, 418, 230);
        do Screen.drawRectangle(340, 25, 343, 230);
        do Screen.drawRectangle(280, 85, 480, 88);
        do Screen.drawRectangle(280, 170, 480, 173);
        return;
    }
    // Draws the symbol 'X' or 'O' at a certain location in the board.
    method void drawSymbol(char symbol, int location)
    {
        var int xr, yr, x, y;
        var Array xCoordinates;
        var Array yCoordinates;
        // Some hard coded coordinates for the board.
        // X-Coordinates.
        let xCoordinates = Array.new(9);
        let yCoordinates = Array.new(9);
        let xCoordinates[0] = 305;
        let xCoordinates[1] = 380;
        let xCoordinates[2] = 450;
        let xCoordinates[3] = 307;
        let xCoordinates[4] = 382;
        let xCoordinates[5] = 452;
        let xCoordinates[6] = 305;
        let xCoordinates[7] = 380;
        let xCoordinates[8] = 450;
        // Y-Coordinates.
        let yCoordinates[0] = 50;
        let yCoordinates[1] = 50;
        let yCoordinates[2] = 50;
        let yCoordinates[3] = 127;
        let yCoordinates[4] = 127;
        let yCoordinates[5] = 127;
        let yCoordinates[6] = 200;
        let yCoordinates[7] = 200;
        let yCoordinates[8] = 200;
        let xr = 20;
        let yr = 20;
        let x = xCoordinates[location];
        let y = yCoordinates[location];
        if (symbol = 79) // If the symbol is 'O'
        {
        do Screen.setColor(true);
        do Screen.drawCircle(x, y, 25);
        do Screen.setColor(false);
        do Screen.drawCircle(x, y, 23);
        }
        if (symbol = 88) // if the symbol is 'X'
        {
        do Screen.setColor(true);
        do Screen.drawLine(x - xr, y - yr, x + xr, y + yr);
        do Screen.drawLine(x - xr, y + yr, x + xr, y - yr);
        }
        return;
    }
    /* Checks if there is a winner */
    method boolean isWinning()
    {
        // Checking Horiziontal wins
        if((equals3(board[0], board[1], board[2])) | (equals3(board[3], board[4], board[5])) | (equals3(board[6], board[7], board[8])))
        {
            return true;
        }
        // Checking Vertical wins.
        if ((equals3(board[0], board[3], board[6])) | (equals3(board[1], board[4], board[7])) | (equals3(board[2], board[5], board[8])))
        {
            return true;
        }
        // Checking Diagonal wins.
        if ((equals3(board[0], board[4], board[8])) | (equals3(board[2], board[4], board[6])))
        {
            return true;
        }
        return false;
    }
    // Takes the contents of a row, or a column, or a diagonal and checks if they are all nonzero and equal, thus a win
    method boolean equals3(int x, int y, int z)
    {
        if((x = 0) | (y = 0) | (z = 0))
        {
            return false;
        }
        if((x = y) & (y = z))
        {
            do Output.printChar(x);
            do Output.printString(" Wins!");
            do Output.println();
            return true;
        }
        return false;
    }
     // Checks if a certain index is empty.
    method boolean isEmpty(int index)
    {
        if (~(board[index] = 79) & ~(board[index] = 88))
        {
            return true;
        }
        else
        {
            return false;
        }
    }
    // Returns the total number of empty cells.
    method int emptyCellsNum() 
    {
        var int i, result;
        let i = 0;
        let result = 0;
        while(i<9)
        {
            if (isEmpty(i))
            {
                let result = result + 1;
            }
            let i = i + 1;
        }
        return result;
    }
    // Makes a move by user.
    method void userMove(char symbol) 
    { 
        var int userchoice;
        var boolean played;
        let played = false;
        let userchoice = -1;
        while(~played)
        {
            while((userchoice < 0) | (userchoice > 8))
            {
                do Output.println();
                do Output.printString("Enter cell number [1 - 9]: ");
                let userchoice = Keyboard.readInt("") - 1;
            }
            if (isEmpty(userchoice))
            {
                do drawSymbol(symbol, userchoice);
                let board[userchoice] = symbol;
                let played = true;
            }
            else
            {
                do Output.println();
                do Output.printString("Cell Already Occupied.");
                do Output.println();
                let userchoice = -1;
            }
        }
        return;
    }
     // Makes a random move.
    method void randomMove(char symbol)
    {
        var boolean played;
        var int randomIndex;
        let played = false;
        while(~played)
        {
            let randomIndex = Random.randRange(8);
            if (isEmpty(randomIndex))
            {
                do drawSymbol(symbol, randomIndex);
                let board[randomIndex] = symbol;
                let played = true;
            }
        }
        return;
    }
    // Plays the game.
    method void play(char gameMode, char userTurn, char userSymbol, char opponentSymbol)
    {
        var boolean winner;
        let winner = false;
        if (gameMode = 49) // Single player.
        {
            if (userTurn = 89) // player-1 starts the game.
            {
                while(loopCondition(winner))
                {
                    do userMove(userSymbol);
                    let winner = isWinning();
                    if (loopCondition(winner))
                    {
                        do randomMove(opponentSymbol);
                        let winner = isWinning();
                    }
                }
            }
            else // player-2 starts the game.
            {   while(loopCondition(winner))
                {
                    do randomMove(opponentSymbol);
                    let winner = isWinning();
                    if (loopCondition(winner))
                    {
                        do userMove(userSymbol);
                        let winner = isWinning();
                    }
                }
            }
        }
        if (gameMode = 50) // Two players. 
        {
            if (userTurn = 89) // If player-1 starts the game.
            {
                while(loopCondition(winner))
                {
                    do userMove(userSymbol);
                    let winner = isWinning();
                    if (loopCondition(winner))
                    {
                        do userMove(opponentSymbol);
                        let winner = isWinning();
                    }
                }
            }
            else // player-2 starts the game.
            {
                while(loopCondition(winner))
                {
                    do userMove(opponentSymbol);
                    let winner = isWinning();
                    if (loopCondition(winner))
                    {
                        do userMove(userSymbol);
                        let winner = isWinning();
                    }

                }
            }
        }
        if (~winner & (emptyCellsNum() = 0)) // if there is no winner and no empty cells, thus a tie.
        {
            do Output.println();
            do Output.printString("Tie!");
        }
        return; 
    }
    method boolean loopCondition(boolean winner) // Evaluates the looping conidition, Used to not make looping condition redundant.
    {
        return ~winner & (emptyCellsNum() > 0);
    } 
}  
