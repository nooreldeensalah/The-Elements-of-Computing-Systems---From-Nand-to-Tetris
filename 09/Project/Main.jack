class Main
{
    function void main() 
    {
        var char userTurn, userSymbol, opponentSymbol, gameMode;
        var Array choices, gameBoard;
        var boolean winner;
        /* Splash screen to adjust the input delay as the random seed */
        do Main.splashScreen();
        /* Accepting input from user */
        let choices = Main.userPrompt();
        let userTurn = choices[0];
        let userSymbol = choices[1];
        let gameMode = choices[2];
        /* Clearing the screen and resetting cursor location */
        do Screen.clearScreen();
        do Output.moveCursor(0,0);
        /* Drawing the border */
        do Board.drawBorder();
        if (userSymbol = 88) {let opponentSymbol = 79;} else {let opponentSymbol = 88;}
        /* Creating an empty board (9 cells array) */
        let gameBoard = Board.createEmptyBoard();
        let winner = false;
        if (gameMode = 49) // Single player.
        {
            if (userTurn = 89) // If player-1 starts the game.
            {
                while(Main.loopCondition(gameBoard, winner))
                {
                    do Board.userMove(gameBoard, userSymbol);
                    let winner = Board.isWinning(gameBoard);
                    if (Main.loopCondition(gameBoard, winner))
                    {
                        do Board.randomMove(gameBoard, opponentSymbol);
                        let winner = Board.isWinning(gameBoard);
                    }
                }
            }
            else // player-2 starts the game.
            {   while(Main.loopCondition(gameBoard, winner))
                {
                    do Board.randomMove(gameBoard, opponentSymbol);
                    let winner = Board.isWinning(gameBoard);
                    if (Main.loopCondition(gameBoard, winner))
                    {
                        do Board.userMove(gameBoard, userSymbol);
                        let winner = Board.isWinning(gameBoard);
                    }
                }
            }
        }
        if (gameMode = 50) // Two players. 
        {
            if (userTurn = 89) // If player-1 starts the game.
            {
                while(Main.loopCondition(gameBoard, winner))
                {
                    do Board.userMove(gameBoard, userSymbol);
                    let winner = Board.isWinning(gameBoard);
                    if (Main.loopCondition(gameBoard, winner))
                    {
                        do Board.userMove(gameBoard, opponentSymbol);
                        let winner = Board.isWinning(gameBoard);
                    }
                }
            }
            else // player-2 starts the game.
            {
                while(Main.loopCondition(gameBoard, winner))
                {
                    do Board.userMove(gameBoard, opponentSymbol);
                    let winner = Board.isWinning(gameBoard);
                    if (Main.loopCondition(gameBoard, winner))
                    {
                        do Board.userMove(gameBoard, userSymbol);
                        let winner = Board.isWinning(gameBoard);
                    }

                }
            }
        }
        if (~winner & (Board.emptyCellsNum(gameBoard) = 0)) // if there is no winner and no empty cells, thus a tie.
        {
            do Output.println();
            do Output.printString("Tie!");
        }
        return;
        
    }
    function Array userPrompt() // Interactive input with user, takes input values, validates them, stores them into an array then returns it.
    {
        var Array choices;
        let choices = Array.new(3);
        do Output.printString("Do you want to play first?");
        while (~(choices[0]=78) & ~(choices[0]=89)) // if choice[0] = 'Y' or 'N'
        {
            do Output.println();
            do Output.printString("Type 'Y' for yes or 'N' for no");
            do Output.println();
            let choices[0] = Keyboard.readChar("");
        }
        while(~(choices[1]=79) & ~(choices[1]=88)) // if choices[1] = 'X' or 'O'
        {
            do Output.println();
            do Output.printString("Choose your symbol 'X' or 'O'");
            do Output.println();
            let choices[1] = Keyboard.readChar("");
        }
        do Output.println();
        do Output.printString("Choose game mode");
        while(~(choices[2]=49) & ~(choices[2]=50)) // If choices[2] = '1' or '2' (the ASCII equivalent of 1 and 2)
        {
            do Output.println();
            do Output.printString("For single player mode: Type '1'");
            do Output.println();
            do Output.printString("For two players mode: Type '2'");
            do Output.println();
            let choices[2] = Keyboard.readChar("");
        }
        return choices;
    }
    function boolean loopCondition(Array board, boolean winner) // Evaluates the looping conidition, Used to not make looping condition redundant.
    {
        return ~winner & (Board.emptyCellsNum(board) > 0);
    }  
    function void splashScreen()
    {
        var int randSeed;
        do Output.moveCursor(11, 23);
        do Output.printString("Welcome to TicTacToe!");
        do Output.moveCursor(12,20);
        do Output.printString("Press any key to continue...");
        do Output.moveCursor(18,19);
        do Output.printString("Written by @nooreldeensalah");
        while (~Keyboard.keyPressed())
        {
            let randSeed = randSeed + 1;
        }
        do Screen.clearScreen();
        do Output.moveCursor(0,0);
        do Random.setSeed(randSeed);
        return;
    }

}